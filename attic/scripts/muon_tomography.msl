COMMENT "Muon tomography scan"
PARAM time, "Seconds per point"
PARAM nsteps, "Number of steps"
PARAM startstep, "Step to start at (usually 0)"
PARAM startxtal, "Xtal row to start at (usually 0, 1-3)"

ODBSET "/Experiment/Edit On Start/Run Type", S 

x[0] = 9100
x[1] = 21900
x[2] = 34700

y[0] = 27300
y[1] = 31500
y[2] = 35150

n_runs = $nsteps + 1

LOOP j, 3
    IF ($j > startxtal)
        ODBSET /Equipment/ISEL/Variables/Output[1], $y[$j-1]
        WAIT ODBvalue, /Equipment/ISEL/Variables/Output[3], =,  $y[$j-1]
        WAIT seconds, 15
        LOOP i, $n_runs
            IF ($i > $startstep)
                position = $x[0] + ($i-1)*($x[2] - $x[0])/$nsteps
                ODBSET /Equipment/ISEL/Variables/Output[0], $position
                CAT comment, "Sequencer script, muon tomography position scan. Position ", $position
                ODBSET "/Experiment/Edit On Start/Comment", $comment
                WAIT seconds, 15
                WAIT ODBvalue, /Equipment/ISEL/Variables/Output[2], =,  $position 
                IF ($i == 1)
                    WAIT seconds, 60
                ENDIF
                TRANSITION start
                WAIT seconds, $time  
                TRANSITION stop
            ENDIF
        ENDLOOP
    ENDIF
ENDLOOP

ODBSET /Equipment/ISEL/Variables/Output[0], $x[1]
ODBSET /Equipment/ISEL/Variables/Output[1], $y[1]
WAIT seconds, 15